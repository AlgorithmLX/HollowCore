buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7.32'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.20-RC'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "org.jetbrains.kotlin.jvm" version "1.7.20"
    id "org.jetbrains.kotlin.plugin.serialization" version "1.7.20"
}
apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

group = 'ru.hollowhorizon'
version = '1.0'
archivesBaseName = 'hc'

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.

configurations {
    shade
    implementation.extendsFrom shade
}

minecraft {
    mappings channel: 'parchment', version: '2022.03.06-1.16.5'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=hc.mixins.json"
            mods { hollowcore { source sourceSets.main } }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=hc.mixins.json"
            mods { hollowcore { source sourceSets.main } }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'hollowcore', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods { hollowcore { source sourceSets.main } }
        }
    }
}

ext {
    imguiVersion = '1.86.4'
}

mixin {
    add sourceSets.main, "hc.refmap.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url = 'https://zoidberg.ukp.informatik.tu-darmstadt.de/artifactory/public-releases/' }
    maven { url 'https://jitpack.io' }
    flatDir { dirs 'libs' }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.39'

    shade 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.10'

    shade 'org.jetbrains.kotlin:kotlin-scripting-jvm-host:1.8.10'
    shade 'org.jetbrains.kotlin:kotlin-scripting-jvm:1.7.20-RC'

    shade 'org.jetbrains.kotlin:kotlin-compiler-embeddable:1.8.10'
    shade 'org.jetbrains.kotlin:kotlin-script-runtime:1.8.10'
    shade 'org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:1.8.10'
    shade 'org.jetbrains.kotlin:kotlin-scripting-dependencies:1.8.10'
    shade 'org.jetbrains.kotlin:kotlin-scripting-dependencies-maven:1.8.10'

    shade 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    shade 'org.jetbrains.kotlinx:kotlinx-serialization-core:1.5.0'

    shade "com.labymedia:ultralight-java-base:0.4.12"
    shade "com.labymedia:ultralight-java-databind:0.4.12"
    shade "com.labymedia:ultralight-java-gpu:0.4.12"
    shade "com.labymedia:ultralight-java-glfw-opengl-util:0.4.12"

    shade (group: 'org.lwjgl', name: 'lwjgl-assimp', version: '3.2.2'){
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }

    shade 'gnu.trove:trove:1.0.2'

    implementation('com.google.guava:guava') {
        version {
            strictly '21.0'
        }
    }

    shade 'org.cadixdev:lorenz:0.5.8'
    shade 'org.cadixdev:bombe:0.4.4'
    shade 'me.jamiemansfield:string:0.1.0'

    shade 'com.upokecenter:cbor:4.5.2'
}

jar {
    archiveClassifier = ''
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    from { configurations.shade.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes([
                "Specification-Title"     : "hollowcore",
                "Specification-Vendor"    : "Hollow Horizon",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "hc.mixins.json"
        ])
    }

    finalizedBy('reobfJar')
}

shadowJar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    configurations = [project.configurations.shade]

    relocate('gnu.trove', 'ru.hollowhorizon.gnu.trove')

    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}

task devJar(type: Jar) {
    from sourceSets.main.output

    it.archiveClassifier = 'dev'

    manifest {
        attributes([
                "Specification-Title"     : "hollowcore",
                "Specification-Vendor"    : "Hollow Horizon",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "hc.mixins.json"
        ])
    }

    finalizedBy('reobfJar')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    it.archiveClassifier = 'sources'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = Arrays.asList("-Xjvm-default=all", "-Xopt-in=kotlin.RequiresOptIn")
        jvmTarget = "1.8"
        languageVersion = "1.7"
    }
}